plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'

    // Step 2: Next, in your Module Gradle file, apply the JUnit 5 plugin.
    id 'de.mannodermaus.android-junit5'
    id 'kover'
}

// Once the plugin is enabled, you can add JUnit 5 configuration options. Here, I set the test
// lifecycle to 'per_class'. By default, Junit 5 creates a new test class instance for each of your
// functions. This means that if you have 5 test cases inside a class, 5 instances are created in
// memory. By setting the lifecycle to per_class, only one test instance is created, and all of your
// test case functions run inside it with the same memory id. You can also annotate your classes
// on a class-by-class basis, but here, I'm enabling the lifecycle options globally.
junitPlatform {
    configurationParameter("junit.jupiter.testinstance.lifecycle.default", "per_class")
}

def appPropertiesFile = rootProject.file("app.properties")
def appProperties = new Properties()
appProperties.load(new FileInputStream(appPropertiesFile))

android {
    namespace 'com.example.cityapiclient'
    compileSdk 33

    defaultConfig {

        buildConfigField("String", "WEB_CLIENT_ID", appProperties['WEB_CLIENT_ID'])
        buildConfigField("String", "KTOR_IP_ADDR", appProperties['KTOR_IP_ADDR'])

        applicationId "com.example.cityapiclient"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        // 1) Make sure to use the AndroidJUnitRunner, or a subclass of it. This requires a dependency on androidx.test:runner, too!
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 2) Connect JUnit 5 to the runner
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"

        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            aaptOptions {
                ignoreAssetsPattern '!*.ttf'
            }
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/licenses/**']
        }
        resources {
            excludes += ['META-INF/licenses/**', 'META-INF/AL2.0', 'META-INF/LGPL2.1', 'font']
        }
    }

    testOptions {
        packagingOptions {
            jniLibs {
                // https://github.com/mockk/mockk/issues/297#issuecomment-901924678
                useLegacyPackaging = true
            }
        }
    }

}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material3:material3:1.1.0-alpha02'
    implementation "androidx.navigation:navigation-compose:2.5.3"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    //serialization/reflection
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.7.20"

    // window layout
    implementation "androidx.window:window:1.1.0-alpha04"
    implementation "androidx.compose.material3:material3-window-size-class:1.1.0-alpha02"

    // hilt
    implementation 'com.google.dagger:hilt-android:2.43.2'
    kapt 'com.google.dagger:hilt-compiler:2.43.2'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // datastore
    //implementation "androidx.datastore:datastore-preferences-core:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.0-alpha03"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    // ViewModel utilities for Compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.5.1"

    // ktor
    implementation "io.ktor:ktor-client-core:2.2.1"
    implementation "io.ktor:ktor-client-android:2.2.1"
    implementation "io.ktor:ktor-client-content-negotiation:2.2.1"
    implementation "io.ktor:ktor-serialization-kotlinx-json:2.2.1"
    implementation "io.ktor:ktor-client-logging:2.2.1"

    // add this to get rid of: SLF4J: Failed to load class "org.slf4j.impl.StaticLoggerBinder"
    // https://logback.qos.ch/manual/configuration.html
    implementation "ch.qos.logback:logback-classic:1.2.11"

    // google sign in
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4"
    implementation 'com.google.android.gms:play-services-auth:20.4.0'

    // ******* TESTING *******************************************************
    // Step 3: Finally, it's time for module dependencies - and there's a lot of them!
    // First, you'll need to add the jupiter Junit 5 dependencies. Here, I add support for
    // parameterized tests, and also the vintage dependency for JUnit 4 tests.
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    // (Optional) If you need "Parameterized Tests"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.2"
    // (Optional) If you also have JUnit 4-based tests
    testImplementation "junit:junit:4.13.2"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.2"

    // Next, add support for launching coroutines in our tests...
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'

    // Then add the MockEngine that Ktor supplies for the Ktor Client. This is awesome and really
    // powerful; I'll go into how it works in a minute.
    testImplementation "io.ktor:ktor-client-mock:2.2.1"

    // Finally, add the MockK dependency. This library is really easy to use, allowed me to use
    // real code for unit tests, and saved me a ton of extra typing.
    testImplementation "io.mockk:mockk:1.13.3"

    // ******* FOR NEXT TIME / SKIP FOR NOW **********************************************
    // 4) Jupiter API & Test Runner, if you don't have it already
    androidTestImplementation "androidx.test:runner:1.5.1"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"

    // 5) The instrumentation test companion libraries
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.3.0"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.3.0"

    // For instrumented tests.
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.44'
    // ...with Kotlin.
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.44'

    // Koin test
    implementation "io.insert-koin:koin-core:3.2.0"
    androidTestImplementation "io.insert-koin:koin-test:3.2.0"
    androidTestImplementation "io.insert-koin:koin-test-junit5:3.2.0"

    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation "io.mockk:mockk-android:1.13.3"

    androidTestImplementation 'app.cash.turbine:turbine:0.12.1'


    testImplementation project(path: ':sharedTest')
    androidTestImplementation project(path: ':sharedTest')
    // *****************************************************

}